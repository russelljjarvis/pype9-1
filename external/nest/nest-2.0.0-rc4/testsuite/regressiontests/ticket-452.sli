/*
 *  ticket-452.sli
 *
 *  This file is part of NEST
 *
 *  Copyright (C) 2010 by
 *  The NEST Initiative
 *
 *  See the file AUTHORS for details.
 *
 *  Permission is granted to compile and modify
 *  this file for non-commercial use.
 *  See the file LICENSE for details.
 *
 */

/* BeginDocumentation

Name: testsuite::ticket-452 - Ensure that precise simulations give identical results whether stepped or run continuously

Synopsis: (ticket-452) run -> NEST exits if test fails

Description:
Ensure that simulations with precise spike timing yield identical results independent of
whether one simulates the full simulation time without interruption, or splits the simulation
into many small pieces.

The problem was unrelated to the setting of kernel property  /off_grid_spiking
and the spike_detector property  /precise_times.
 
Author: Hans Ekkehard Plesser, 2010-09-30; based on original reproduced code by Alexander Hanuschkin
 */

/unittest (8831) require
/unittest using
M_ERROR setverbosity


/Run
{
 /model Set
 /simcommand Set

 ResetKernel
                                    % 0.125 ms
 0 << /tics_per_ms 3 dexp  /resolution -3 dexp >> SetStatus

 model << /I_e 500.0  >> Create /n Set
 
 /poisson_generator_ps << /rate 10000.0 /start 0.0  >> Create  /pg Set
   
 pg n 1.0 1.0 Connect

 /spike_detector 
     <<
      /withtime true   
      /withgid false
      /precise_times false  % problem was unrelated to setting
    >> Create /sd Set

 n sd Connect

 simcommand

 sd [/events /times] get cva 

} def 


[/iaf_psc_delta_canon /iaf_psc_alpha_canon ]
{
 {Run} exch prepend
 [
  {500.0 Simulate} 
  {1 1 500  {; 1.0 Simulate} for}  
 ]
 exch forall eq 
}
forall 

and assert_or_die

