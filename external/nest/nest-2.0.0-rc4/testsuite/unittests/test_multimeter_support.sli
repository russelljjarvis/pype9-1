/*
 *  test_multimeter_support.sli
 *
 *  This file is part of NEST
 *
 *  Copyright (C) 2010 by
 *  The NEST Initiative
 *
 *  See the file AUTHORS for details.
 *
 *  Permission is granted to compile and modify
 *  this file for non-commercial use.
 *  See the file LICENSE for details.
 *
 */


/* BeginDocumentation
Name: testsuite::test_multimeter_support - test if multimeter support works in neurons

Synopsis: (test_multimeter_support.sli) run -> dies if assertion fails

Description:
The script checks all modes providing a "recordables" entry in their status dict.
It creates a multimeter, sets it to record from all recordables and checks whether
the correct amount of data is collected. It does not check the content of the data
collected.
Author: Plesser
FirstVersion: 2010-10-01
*/

/unittest (8831) require
/unittest using

M_ERROR setverbosity

% Execute test for one model
% Argument: model name
% Result: true or false
% Returns true if model does not have recordables
/run_test
{
  /model Set
  model GetDefaults dup
  /recordables known
  {
     /recordables get /recs Set
     /n model Create def
     /mm /multimeter << /record_from recs /withtime false >> Create def
     mm n Connect
     10.0 Simulate
     mm /events get [ recs ] get { cva length 9 eq } Map
     true exch { and } Fold
  }
  {
    pop
    true
  }
  ifelse
} 
def

{
  modeldict keys { run_test } Map
  true exch { and } Fold
}
assert_or_die

