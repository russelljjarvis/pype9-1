input_freq_AMPA = 0
input_freq_NMDA = 0
input_freq_GABA = 0

//object variable will refer to a list that will hold an arbitrary number of NetCon objects.

objectvar nclist  

//synapses         
objectvar synAMPA,synNMDA,synGABA,synGABAa2 

//counting spikes
objref spiketimes,spikecount

objref voltagem

tmax=1000   

create soma //create compartment

x=0
y=0
z=0

spiketimes=new Vector()
lastspikecount=0

voltagem=new Vector()

soma {

    pt3dclear()
    pt3dadd(x,y,z,10)  //set position of cell
    pt3dadd(x,y,z+10,10)
     
    nseg=1
    diam=11.8
    L=11.8 
    Ra=100
    cm=1
    celsius = 30
    
    Area = PI*soma.diam*soma.L*1e-8

    insert Granule_DeSouza10_ca
    insert Granule_DeSouza10_CaHVA    
    insert Granule_DeSouza10_KA
    insert Granule_DeSouza10_KCa 
    insert Granule_DeSouza10_KM
    insert Granule_DeSouza10_KV
    insert Granule_DeSouza10_Kir
    insert Granule_DeSouza10_Lkg1
    insert Granule_DeSouza10_Lkg2
    insert Granule_DeSouza10_Na 

    gcabar_Granule_DeSouza10_ca= 0.00046    //2009 model
    gkbar_Granule_DeSouza10_KCa = 0.04   //ajusted Version 3
    gkbar_Granule_DeSouza10_KA = 0.0032   //2009 model
    gkbar_Granule_DeSouza10_KV= 0.003    //2009 model
    gkbar_Granule_DeSouza10_KM= 0.00025   //0.0001  //2009 model
    gkbar_Granule_DeSouza10_Kir = 0.0009  //2009 model
    gl_Granule_DeSouza10_Lkg1=5.68e-5   //2009 model
    ggaba_Granule_DeSouza10_Lkg2 = 3e-05    //2009 model
    gnabar_Granule_DeSouza10_Na= 0.013     //2009 model  

    beta_GRC_ca = 1.5
    
    usetable_Granule_DeSouza10_ca   = 0              
    usetable_Granule_DeSouza10_KA   = 0
    usetable_Granule_DeSouza10_KCa  = 0
    usetable_Granule_DeSouza10_KM   = 0
    usetable_Granule_DeSouza10_KV   = 0
    usetable_Granule_DeSouza10_Kir  = 0   
    usetable_Granule_DeSouza10_Na   = 0      
    
    ena = 87.39
    ek = -84.69
    //eca = 129.33
    ecl = -65
    
    nclist=new List()
    
    //Synapses
    synAMPA=new Exp2Syn(0.5) //Adding synapse with rise and decay time
    synAMPA.tau1=0.03  // ms rise time 
    synAMPA.tau2=0.5  // ms decay time 
    synAMPA.e=0     // mV reversal potential 
    
    synNMDA=new Exp2Syn(0.5) //Adding synapse with rise and decay time
    synNMDA.tau1=1  // ms rise time
    synNMDA.tau2=13.3  // ms decay time 
    synNMDA.e=0     // mV reversal potential 
    
    synGABA=new Exp2Syn(0.5) //Adding synapse with rise and decay time
    synGABA.tau1=0.31  // ms rise time 
    synGABA.tau2=8.8  // ms decay time 
    synGABA.e=-75     // mV reversal potential 
    
    //counting spikes
    spikecount=new APCount(0.5)
    spikecount.thresh=-20
    spikecount.record(spiketimes)
    
    //Saving Vm
    voltagem.record(&v(0.5))
}


objref ns_AMPA, nc_AMPA
objref ns_NMDA, nc_NMDA
objref ns_GABA, nc_GABA 

if (input_freq_AMPA != 0) {
  
  ns_AMPA = new NetStim(0.5)
  ns_AMPA.interval = 1000 / input_freq_AMPA
  ns_AMPA.number = input_freq_AMPA * 2
  
  nc_AMPA = new NetCon(ns_AMPA, synAMPA)
  nc_AMPA.weight = 1
  
}

if  (input_freq_NMDA != 0) {
  ns_NMDA = new NetStim(0.5)
  ns_NMDA.interval = 1000 / input_freq_NMDA
  ns_NMDA.number = input_freq_NMDA * 2
  
  nc_NMDA = new NetCon(ns_NMDA, synNMDA)
  nc_NMDA.weight = 1
  
}

if (input_freq_GABA != 0) {
  
  ns_GABA = new NetStim(0.5)
  ns_GABA.interval = 1000 / input_freq_GABA
  ns_GABA.number = input_freq_GABA * 2
  
  nc_GABA = new NetCon(ns_GABA, synGABA)
  nc_GABA.weight = 1
  
}


/*
 General settings
*/ 
v_init = -70

tstop = 1000 //for IClamp
dt = 0.025

secondorder=0  //method implicit backward euler

celsius = 30 
SogliaSpike=0

access soma
psection()

printf("ena: %f", ena)
printf("ek: %f", ek)
printf("eca: %f", eca)
printf("ecl: %f", ecl)
printf("celsius: %f", celsius)
 
objref rec_v
rec_v = new Vector()
rec_v.record (&soma.v(0.5))

objref rec_t
rec_t = new Vector()
rec_t.record (&t)

dt = 0.025
tstop = 2000
run()

objref output_file
output_file = new File()
output_file.wopen("../../../../output/combined.Granule.v_dat")
for (i=0; i < rec_v.size(); i=i+1) {
  output_file.printf("%g %g\n", rec_t.x[i], rec_v.x[i])
}
output_file.close()
quit()

