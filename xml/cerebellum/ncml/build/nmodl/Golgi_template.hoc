/*******Cerebellar Golgi Cell Model **********

Developers:    Sergio Solinas & Egidio D'Angelo
Code contributors:  Thierry Neius, Shyam Diwakar, Lia Forti
Data Analysis: Sergio Solinas

Work Progress: April 2004 - May 2007

Developed At:  Università Degli Studi Di Pavia
	       Dipartimento Di Scienze Fisiologiche
	       Pavia - Italia
	       
Model Published in: 
             Sergio M. Solinas, Lia Forti, Elisabetta Cesana, 
             Jonathan Mapelli, Erik De Schutter and Egidio D`Angelo (2008)
             Computational reconstruction of pacemaking and intrinsic 
             electroresponsiveness in cerebellar golgi cells
             Frontiers in Cellular Neuroscience 2:2


********************************************/

// In this configuration the ion channels 
// were not corrected for the Liquid Junction potential.
// The ion reversal potential were corrected in agreement
// with the voltage shift.
// See Table 1 Solinas et al. 2008 Frontiers Neurosci 2:2

begintemplate GoC
public soma,axon,elec,seal,dend
public SpikeTrain, RT_Vm, EL_Vm

create soma
create axon
create elec,seal
create dend[3]

objref SpikeTrain, netcon, nil
objref RT_Vm, EL_Vm

proc init() {

    RT_Vm = new Vector()
    EL_Vm = new Vector()
    create soma
    soma {
	nseg = 1
	diam = 27 // 22 pF Dieudonne98
	L = 27
	Ra = 100 // From Roth&Hausser2000
	celsius = 23
	
	insert Golgi_Lkg
	
	insert Golgi_Na
	insert Golgi_NaR
	insert Golgi_NaP
	
	insert Golgi_CaHVA
	insert Golgi_CaLVA
		
	insert Golgi_KV
	insert Golgi_KM
	insert Golgi_KA
	
	insert Golgi_KCa
 	insert Golgi_SK2
	
        insert Golgi_HCN1
        insert Golgi_HCN2

	insert Golgi_Ca
	insert Golgi_Ca2
	
	
	cai0_ca_ion = 50e-6
	ca2i0_ca2_ion = cai0_ca_ion
	
	cai0_Golgi_Ca = cai0_ca_ion
	ca2i0_Golgi_Ca2 = cai0_ca_ion
	
	cai = cai0_ca_ion
	ca2i = cai
	ca2o = cao
	
	ena =  87.39
	ek  = -84.69

	SpikeTrain = new Vector()
	netcon = new NetCon(&v(0.5),nil)
	netcon.threshold=-20
	netcon.record(SpikeTrain)
	
	RT_Vm.record(&v(0.5))
    }
    
    create dend[3]
    for i=0,2 {
	dend[i] {
	    nseg = 10
	    diam = 3
	    L = 113
	    Ra = 100
	    celsius = 23
	    
	    insert Golgi_Lkg
	}
	connect dend[i](0), soma(1)	
    }
    
    
    create axon
    axon {
	nseg = 100
	diam = 2.4 // gives 90 pF to get to the 145 pF Forti06
	L = 1200
	Ra = 100
	celsius = 23
	
	insert Golgi_Lkg
    }
    
    connect axon(0), soma(0)
    
    create elec,seal
    elec {
	nseg = 1
	diam = 3
	L = 1000
	Ra = 36
	cm = 0.0015
	celsius = 23
	EL_Vm.record(&v(0.5))

    }
        
    seal {
	nseg = 1
	diam = 3
	L = 1
	Ra = 1
	cm = 0.0001
	celsius = 23
	
    }
       
    connect seal(1), soma(1)
    connect elec(1), seal(0)

}

		    
endtemplate GoC
